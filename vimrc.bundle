" Description: Vim bundle configuration.
" Author: Bohr Shaw <pubohr@gmail.com>

" BundleManagement: {{{1

" Define an empty auto-command group
execute 'augroup bundle| autocmd!'

" Move between bundles, or open a bundle's URL in the browser
autocmd BufReadPost {.,}vimrc.bundle
      \ nnoremap <buffer> <silent> ]g :call search('Bundles\=(.\zs\C')<CR>|
      \ nnoremap <buffer> <silent> [g :call search('Bundles\=(.\zs\C', 'b')<CR>

" A minimal vimrc for test and reference
if Bundles('tpope/vim-sensible')
  let g:loaded_sensible = 1 " disable this bundle
endif

" A Vim plugin for Vim plugins
call Bundles('tpope/vim-scriptease')

" MotionSearch: {{{1

" The missing motion for Vim
if Bundles('+justinmk/vim-sneak')
  " let g:sneak#streak = 1
  " let g:sneak#use_ic_scs = 1 " 'ignorecase', 'smartcase'
  " Alternative mappings: z; z,
  nmap f<CR> <Plug>SneakForward
  xmap f<CR> <Plug>VSneakForward
  nmap f<BS> <Plug>SneakBackward
  xmap f<BS> <Plug>VSneakBackward
  nmap t<CR> <Plug>(SneakStreak)
  nmap t<BS> <Plug>(SneakStreakBackward)
  hi! link SneakPluginScope Comment
endif

" Vim motions on speed
if Bundles('-bohrshaw/vim-easymotion')
  let g:EasyMotion_leader_key = 'm<Tab>'
endif

" Acts like `f` but taking two characters
if Bundles('-goldfeld/vim-seek')
  let g:SeekKey = 'f<CR>'
  let g:SeekBackKey = 'f<BS>'
  " let g:seek_enable_jumps = 1
endif

" Extended f, F, t and T key mappings for Vim
if Bundles('-rhysd/clever-f.vim')
  let g:clever_f_across_no_line = 1
endif

" Create your own text objects
call Bundles('kana/vim-textobj-user')

" Start a * or # search from a visual block
call Bundles('nelstrom/vim-visual-star-search')

" Insert: {{{1

" Software caps lock
if Bundles('tpope/vim-capslock')
  imap <C-L> <Plug>CapsLockToggle
endif

" Provides insert mode auto-completion for quotes, parens, brackets, etc.
call Bundles('Raimondi/delimitMate')

" Wisely add 'end' in ruby, endfunction/endif/more in vim script, etc
call Bundles('tpope/vim-endwise')

" A code-completion engine for Vim
" call Bundles('Valloric/YouCompleteMe')

" Perform all your vim insert mode completions with Tab
" call Bundles('ervandew/supertab')

" The ultimate snippet solution
if Bundle('SirVer/ultisnips', {'m': ['i <C-J>'], 'f': '*.snippets'}) &&
      \ Bundles('honza/vim-snippets')
  let g:UltiSnipsExpandTrigger='<C-J>'
  let g:UltiSnipsListSnippets='<C-Q>'
  let g:UltiSnipsJumpForwardTrigger="<C-J>"
  let g:UltiSnipsJumpBackwardTrigger="<C-Z>"
  let g:UltiSnipsSnippetDirectories=["UltiSnips", "snippet"]
  let g:UltiSnipsSnippetsDir="~/.vim/snippet" " personal snippets path
  " let g:UltiSnipsEditSplit="vertical"
endif

" call Bundles(['garbas/vim-snipmate', 'MarcWeber/vim-addon-mw-utils',

"       \ 'tomtom/tlib_vim', 'honza/vim-snippets'])

" Change: {{{1

" Commenting
if Bundles('+tpope/vim-commentary')
  let commentary_map_backslash = 0
endif

call Bundles('-scrooloose/nerdcommenter', '-tomtom/tcomment_vim')

" Quoting/parenthesizing made simple
call Bundles('+tpope/vim-surround')

" Text filtering and alignment
call Bundles('godlygeek/tabular', '-tommcdo/vim-lion')

" Use CTRL-A/CTRL-X to increment dates, times, and more
call Bundles('+tpope/vim-speeddating')

" Punctuation text objects: ci/ da; vi@ yiq da<space> ..
call Bundles('kurkale6ka/vim-pairs')

" Exchange text flexibly with a text exchange operator
if Bundles('+tommcdo/vim-exchange')
  " nmap cx <Plug>(Exchange)
  vmap cx <Plug>(Exchange)
  " nmap cxc <Plug>(ExchangeClear)
  " nmap cxx <Plug>(ExchangeLine)
endif

" Exchange(swap) text directly/quickly with mappings
if Bundles('+kurkale6ka/vim-swap')
  " let g:swap_custom_ops = []
  vmap cq  <plug>SwapSwapOperands
  vmap cp <plug>SwapSwapPivotOperands
  nmap cq  <plug>SwapSwapWithR_WORD
  nmap cQ  <plug>SwapSwapWithL_WORD
endif

" Easily search for, substitute, and abbreviate multiple variants of a word
call Bundles('+tpope/vim-abolish')

" True Sublime Text style multiple selections for Vim
call Bundles('terryma/vim-multiple-cursors')

" Characterize.vim: Unicode character metadata
call Bundles('tpope/vim-characterize')

" History: {{{1

" Display your undo history in a graph
if Bundles('-mbbill/undotree')
  nnoremap <BS>u :UndotreeToggle<CR>
  let g:undotree_SetFocusWhenToggle=1 " put cursor to undotree when togglling
endif

" Vim plugin to visualize your Vim undo tree
if Bundles('sjl/gundo.vim')
  nnoremap <BS>u :GundoToggle<CR>
endif

" Enable repeating supported plugin maps with '.'
call Bundles('+tpope/vim-repeat')

" Maintains a history of previous yanks, changes and deletes
" call Bundles('vim-scripts/yankring.vim')

" Mapping: {{{1

" Pairs of handy bracket mappings
call Bundles('+tpope/vim-unimpaired')

" Mappings for simultaneously pressed keys
" call Bundles('kana/vim-arpeggio')

" Fixes key codes for console Vim
" call Bundles('drmikehenry/vim-fixkey')

" FileBufferPaneWindowTabSession: {{{1

" Fuzzy file, buffer, mru, tag, etc finder
if Bundles('kien/ctrlp.vim')
  let g:ctrlp_cache_dir = $HOME.'/.vim/tmp/ctrlp'
  " Set the mode to determine the root searching directory.
  " let g:ctrlp_working_path_mode = 'ra'
  " let g:ctrlp_show_hidden = 1 " scan for dotfiles and dotdirs
  " let g:ctrlp_clear_cache_on_exit = 0
  let g:ctrlp_max_files = 0
  let g:ctrlp_lazy_update = 200 " update the match window lazily
  let g:ctrlp_by_filename = 1 " search by file name only
  let g:ctrlp_custom_ignore = {
        \ 'dir':  '\v[\/]\.(git|hg|svn)$',
        \ 'file': '\v\.(exe|so|dll)$'
        \ }
  " Specify an external tool to use for listing files.
  let g:ctrlp_user_command = {
        \ 'types': {
        \ 1: ['.git', 'cd %s && git ls-files && git ls-files -o --exclude-standard'],
        \ 2: ['.hg', 'hg --cwd %s locate -I .'],
        \ },
        \ }
  if has('win32')
    let g:ctrlp_user_command['types'][1][1] = '('.g:ctrlp_user_command['types'][1][1].')'
  endif
  if executable('ag')
    let g:ctrlp_user_command['fallback'] = 'ag %s -l --nocolor -g ""'
  endif
  " Bring up this finder
  let g:ctrlp_map = '<BS>f'
  " Clear the cache of the current search path
  noremap <leader>fc :CtrlPClearCache<cr>
  " Open CtrlP bookmarked directories
  noremap <leader>fm :CtrlPBookmarkDir<cr>
  " Extensions
  let g:ctrlp_extensions = ['funky']
  " Fast vim CtrlP matcher based on python, for large projects
  if Bundles('FelikZ/ctrlp-py-matcher') && has('python')
    let g:ctrlp_match_func = {'match': 'pymatcher#PyMatch'}
  endif
  " Navigate and jump to function defs
  if Bundles('tacahiroy/ctrlp-funky')
    nnoremap <Leader>ff :CtrlPFunky<CR>
  endif
endif

" A tree explorer plugin for vim
if Bundle('scrooloose/nerdtree', {'m': [
      \ 'nnoremap <BS>d :NERDTreeToggle<CR>',
      \ 'noremap <leader>dd :NERDTree<CR>',
      \ 'noremap <leader>df :NERDTreeFind<CR>']})
  let NERDTreeHijackNetrw=0 " don't replace netrw
  let NERDTreeBookmarksFile=$HOME.'/.vim/tmp/NERDTreeBookmarks'
  let NERDTreeIgnore=['^\.$', '^\.\.$', '\~$', '\.pyc$', '\.swp$']
  let NERDTreeShowHidden=1
  let NERDTreeShowBookmarks=1
  let NERDTreeQuitOnOpen=1
  let NERDTreeMouseMode=2
endif

" Combine with netrw to create a delicious salad dressing
if Bundles('tpope/vim-vinegar')
  " Change the default mapping
  nnoremap - -
  autocmd VimEnter * nunmap -
  nmap d<Space> <Plug>VinegarUp
endif

" Changes Vim working directory to project root
if Bundles('airblade/vim-rooter')
  let g:rooter_manual_only = 1
  " let g:rooter_use_lcd = 1
  nmap <silent> <Leader>dc <Plug>RooterChangeToRootDirectory
endif

" Buffer Explorer/Browser
call Bundles('vim-scripts/bufexplorer.zip', '-jeetsukumaran/vim-buffergator')

" Delete buffers in Vim without closing your windows or messing up your layout
if Bundles('moll/vim-bbye')
  nnoremap <Leader>bd :Bdelete<CR>
endif

" Distraction-free writing
if Bundle('junegunn/goyo.vim', {'m': ['noremap <C-W>O :Goyo<CR>']})
  " let g:goyo_width = 100
  let g:goyo_margin_top = 2
  let g:goyo_margin_bottom = 2
  " let g:goyo_linenr = 1
endif

" Automatic resizing of Vim windows to the golden ratio
if Bundles('roman/golden-ratio')
  let g:golden_ratio_autocommand = 0
  noremap <C-W>G :GoldenRatioResize<CR>
endif

" A start screen for Vim
if Bundles('mhinz/vim-startify')
  let g:startify_session_dir = '~/.vim/tmp/session'
  let g:startify_list_order = ['sessions', 'bookmarks', 'files']
  let g:startify_skiplist = ['[Vv]im.*[\/]doc[\/][^\/]\+\.txt']
  autocmd FileType startify setlocal nospell
  " Prevent CtrlP open a split
  autocmd FileType startify setlocal buftype=
  let g:startify_custom_header = [
        \ '   _   /|',
        \ "   \\'o.O'",
        \ '   =(___)=',
        \ '      U    Vim --resembo!',
        \ ''
        \ ]
endif

" Vim session manager
if Bundles('bohrshaw/vim-mansion')
  let g:sessiondir = '~/.vim/tmp/session'
  " let g:mansion_no_auto_save = 1
  " let g:mansion_no_maps = 1
endif

" Appearance: {{{1

" Color Schemes {{{2

" Precision colorscheme for the vim text editor
if Bundles('+altercation/vim-colors-solarized')
  let g:solarized_italic = 0
  let g:solarized_termcolors = &term =~ '256col' ? 256 : 16
  let g:solarized_menu=0
  autocmd ColorScheme solarized runtime colors/solarized_additional.vim
endif

call Bundles('+chriskempson/base16-vim', '+tomasr/molokai', '+zeis/vim-kolor',
      \ 'w0ng/vim-hybrid', 'sk1418/last256', 'nanotech/jellybeans.vim',
      \ 'junegunn/seoul256.vim', 'reedes/vim-colors-pencil',
      \ '-tpope/vim-vividchalk')

" Sublime Text 2 Themes (textmate)
call Bundles('+daylerees/colour-schemes/vim-themes')

" One colorscheme pack to rule them all
" call Bundles('flazz/vim-colorschemes')

" Make gvim-only colorschemes work transparently in terminal vim
" call Bundles('godlygeek/csapprox')

" }}}2

" Lean & mean statusline for vim that's light as air
if Bundles('-bling/vim-airline')
  " Remove separators, the different colors already make it easy to distinguish.
  let [g:airline_left_sep, g:airline_right_sep] = ['', '']
  " let g:airline_paste_symbol = 'P'
  let g:airline_section_z = '%l,%c %p%%' "right side section
  " Use shorter modes indicators
  let g:airline_mode_map = { '__': '-', 'n': 'N', 'i': 'I', 'R': 'R', 'c': 'C',
        \ 'v': 'V', 'V': 'VL', '': 'VB', 's': 'S', 'S': 'SL', '': 'SB'}
  " Extensions
  " Disable showing a summary of changed hunks under source control.
  let g:airline#extensions#hunks#enabled = 0
  " Showing only non-zero hunks.
  let g:airline#extensions#hunks#non_zero_only = 1
  " Disable detection of whitespace errors.
  let g:airline#extensions#whitespace#enabled = 0
  " Disable tagbar integration.
  let g:airline#extensions#tagbar#enabled = 0
endif

" Super simple vim plugin to show the list of buffers in the command bar
" call Bundles('bling/vim-bufferline')

" Toggle, display and navigate marks
if Bundles('-kshenoy/vim-signature')
  let g:SignatureEnabledAtStartup = 0
  let g:SignatureMenu = 0
endif

" A plugin that shows a (git)diff via Vim's sign column
if Bundles('-mhinz/vim-signify', '-airblade/vim-gitgutter')
  " let g:signify_disable_by_default = 1
  let g:signify_vcs_list = ['git']
  let g:signify_sign_delete = '-'
endif

" Displaying indent levels visually
if Bundles('Yggdroot/indentLine', '-nathanaelkane/vim-indent-guides')
  let g:indentLine_enabled = 0
  nnoremap <BS>i :IndentLinesToggle<CR>
  let g:indent_guides_guide_size = 1
endif

" FileTypes: {{{1

" Mixed {{{2

" Syntax checking hacks for vim
if Bundles('scrooloose/syntastic')
  let g:syntastic_mode_map = { 'mode': 'active',
        \ 'active_filetypes': [],
        \ 'passive_filetypes': [] }
endif

" Vim plugin that displays tags in a window, ordered by class etc
if Bundles('majutsushi/tagbar')
  nnoremap <BS>t :TagbarToggle<CR>
endif

" Documentation/reference viewer
call Bundles('Keithbsmiley/investigate.vim')

if Bundles('-thinca/vim-ref')
  let g:ref_no_default_key_mappings = 1
  nmap <silent> gK <Plug>(ref-keyword)
  vmap <silent> gK <Plug>(ref-keyword)
endif

" Dispatch.vim: asynchronous build and test dispatcher
call Bundles('tpope/vim-dispatch')

" Run commands quickly
if Bundles('thinca/vim-quickrun')
  let g:quickrun_no_default_key_mappings = 1
  map <BS>r <Plug>(quickrun)
endif

" Transition between multiline and single-line code
if Bundles('AndrewRadev/splitjoin.vim')
  " let g:splitjoin_split_mapping = 'gS'
  " let g:splitjoin_join_mapping  = 'gJ'
endif

" Markups {{{2

" HTML5 omnicomplete and syntax
call Bundles('othree/html5.vim')

" Runtime files for Haml, Sass, and SCSS
call Bundles('tpope/vim-haml')

" Runtime files for LESS (dynamic CSS)
call Bundles('groenewege/vim-less')

" Improves HTML & CSS workflow: http://emmet.io
if Bundles('mattn/emmet-vim')
  " Only enable insert mode functions
  let g:user_emmet_mode='i'
  " let g:user_emmet_leader_key = '<C-Z>'
  " Enable just for certain file types
  " let g:user_emmet_install_global = 0
  " autocmd FileType html,css EmmetInstall
endif

" Markdown runtime files
call Bundles('tpope/vim-markdown')

" Vim, your markup language of choice, and your browser of choice
" call Bundles('matthias-guenther/hammer.vim')

" Javascript {{{2

" Vastly improved Javascript indentation and syntax support
call Bundles('pangloss/vim-javascript')

" A plugin that integrates JSHint with Vim
" call Bundles('walm/jshint.vim')

" Tern plugin for vim(provides Tern-based JavaScript editing support)
" call Bundles('marijnh/tern_for_vim')

" CoffeeScript support for vim
call Bundles('kchmck/vim-coffee-script')

" Ruby {{{2

" Ruby runtime files (shipped with Vim and fairly updated.)
" call Bundles('vim-ruby/vim-ruby')

" A custom text object for selecting ruby blocks
call Bundle('nelstrom/vim-textobj-rubyblock',
      \ {'f':'ruby', 'd':['kana/vim-textobj-user']})

" Provide more controls over the above
if Bundles(['-rhysd/vim-textobj-ruby', 'kana/vim-textobj-user'])
  let g:textobj_ruby_more_mappings = 1
endif

" Runs RuboCop(A robust Ruby code analyzer) and displays the results in Vim
if Bundles('ngmy/vim-rubocop')
  let g:vimrubocop_keymap = 0
  autocmd FileType ruby nnoremap <silent> <buffer> <nowait>
        \ <LocalLeader>r :RuboCop<CR>
endif

" Bindings for the gem recording the results of every line of code
if Bundles('hwartig/vim-seeing-is-believing')
  autocmd FileType ruby
        \ NXInoremap <buffer> <F5> <Plug>(seeing-is-believing-run)|
        \ NXInoremap <buffer> <F4> <Plug>(seeing-is-believing-mark)
endif

" Vim plugin for debugging Ruby applications (using ruby-debug-ide gem)
" call Bundles('astashov/vim-ruby-debugger')

" It's like rails.vim without the rails
call Bundle('tpope/vim-rake', {'f':'ruby'})

" Python {{{2

" Vim python-mode. PyLint, Rope, Pydoc, breakpoints from box.
if Bundles('klen/python-mode')
  let g:pymode_run_bind = '<LocalLeader>r'
  let g:pymode_breakpoint_bind = '<LocalLeader>b'
  let g:pymode_rope = 0
  " Escape syntastic check for python
  call add(g:syntastic_mode_map['passive_filetypes'], 'python')
  let g:pymode_python = 'python3'
endif

" Bindings for the python auto-completion library 'jedi'
if Bundles('davidhalter/jedi-vim')
  let g:jedi#auto_vim_configuration = 0
  autocmd FileType python setlocal completeopt=menuone,longest,preview
  let g:jedi#goto_assignments_command = '<LocalLeader>d'
  let g:jedi#goto_definitions_command = '<LocalLeader>D'
  let g:jedi#rename_command = '<LocalLeader>R'
  let g:jedi#usages_command = '<LocalLeader>U'
  let g:jedi#use_tabs_not_buffers = 0 " show definition in the current window
  let g:jedi#squelch_py_warning = 1 " suppress warning of -python
  let g:jedi#force_py_version = 3
endif

" A two-way integration between Vim and IPython 0.11+
call Bundles('-ivanov/vim-ipython')

" Java {{{2

" My work on integration of Scala into Vim - not a ton here, but useful for me
" call Bundles('derekwyatt/vim-scala')

" Others {{{2

" Vim Git runtime files
call Bundles('+tpope/vim-git')

" Github mirror of Go vimscripts, synced with main repository http://golang.org
call Bundles('jnwhiteh/vim-golang')

" A Vim plugin for Windows PowerShell support
call Bundles('PProvost/vim-ps1')

" Syntax file for nginx
call Bundles('vim-scripts/nginx.vim')

" A mirror to the official vimperator's syntax file
call Bundles('bohrshaw/vim-vimperator-syntax')

" ExternalInteraction: {{{1

" A Git wrapper so awesome, it should be illegal.
if Bundles('tpope/vim-fugitive')
  nnoremap <silent> <Leader>gs :Gstatus<CR>
  nnoremap <silent> <Leader>gd <C-W>o:Gdiff<CR>
  nnoremap <silent> <Leader>gc :Gcommit -v<CR>
  nnoremap <silent> <Leader>gb :Gblame<CR>
  nnoremap <silent> <Leader>gl :Glog<CR>
  nnoremap <silent> <Leader>gp :Git push<CR>
endif

" Helpers for UNIX(Windows)
call Bundles('tpope/vim-eunuch')

" Tmux basics
if !has('win32') && Bundles('tpope/vim-tbone')
endif

" Implement transparent editing of gpg encrypted files
" call Bundles('jamessan/vim-gnupg')

" Open URI with your favorite browser
if Bundles('tyru/open-browser.vim')
  autocmd BufReadPost {.,}vimrc.bundle nnoremap <buffer> <silent> gB
        \ :execute 'silent OpenBrowser https://github.com/'
        \ .matchstr(getline('.'), 'Bundles\?([''"][+-]\?\zs.\{-}\ze[''"]')<CR>
endif

" Vimscript for gist
call Bundles(['mattn/gist-vim', 'mattn/webapi-vim'])

" Run code on codepad.org
call Bundles(['mattn/codepad-vim', 'mattn/webapi-vim'])

" Interact with the simplenote service
" call Bundles('mrtazz/simplenote.vim')

" All 256 xterm colors with their RGB equivalents, right in Vim!
call Bundles('guns/xterm-color-table.vim')

" AlternativeUsages: {{{1

" Personal Wiki for Vim
if Bundles('-vimwiki/vimwiki')
  " Restrict vimwiki's operation to only those paths listed in g:vimwiki_list.
  let g:vimwiki_global_ext = 0
  " Don't conceal characters
  let g:vimwiki_conceallevel = 0
  " Register one or more wikis
  let g:vimwiki_list = [{'path': '~/vimwiki/',
        \ 'syntax': 'markdown',
        \ 'ext': '.md'}]
endif

if Bundles('itchyny/calendar.vim')
  " let g:calendar_google_calendar = 1
  " let g:calendar_google_task = 1
endif

" Task managers {{{2

" Quicktask is a lightweight Vim task management plugin
" call Bundles('aaronbieber/quicktask')

" Vim plugin for the TaskPaper application
" call Bundles('davidoc/taskpaper.vim')

" Vim Syntax for todo.txt (todotxt.com)
" call Bundles('davidoc/todo.txt-vim')

" BundleManagement: {{{1

execute 'augroup END'

" vim:ft=vim tw=80 et sw=2 fdm=marker cms="\ %s nowrap:
